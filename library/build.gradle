apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
}

apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.0"

ext.isReleaseVersion = has("release")

if (isReleaseVersion) {
    ext.sonatypeRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    version += "-SNAPSHOT"
    ext.sonatypeRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
}

group = "com.gzonevn.sondt"

ext.sonatypeUsername = has("sonatypeUsername") ? sonatypeUsername : ""
ext.sonatypePassword = has("sonatypePassword") ? sonatypePassword : ""

configurations {
    archives {
        extendsFrom configurations.default
    }
}

repositories {
    mavenCentral()
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task publishLocal(type: Upload) {
    configuration = configurations.archives
    repositories.mavenDeployer {
        repository(url: "file://$buildDir/repo")
    }
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: project.ext.sonatypeRepo) {
            authentication(userName: project.ext.sonatypeUsername,
                    password: project.ext.sonatypePassword)
        }

        pom.project {
            name 'Android Font Text view Library'
            packaging 'aar'
            description 'Customize font library for Android applications'
            url 'https://github.com/sondt87/FontTextView'

            scm {
                url 'scm:git@github.com:sondt87/FontTextView.git'
                connection 'scm:git@github.com:sondt87/FontTextView.git'
                developerConnection 'scm:git@github.com:sondt87/FontTextView.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'son.dt'
                    name 'Son Duong Thanh'
                    email 'son.dt87@gmail.com'
                }
            }
        }
    }
}